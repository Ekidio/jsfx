// Volume Range Trim
//
// Volume adjustment in a selectable +/- Decibel range for
// more controlled automation moves.
//
// author: chokehold
// url:    https://github.com/chkhld/jsfx/
// tags:   utility gain range volume trim
//
desc: Volume Range Trim

slider1:amount=0<-1,1,0.0001>Trim amount
slider2:range=0<0,3,1{6 dB,12 dB, 24 dB, 48 dB}>Range +/-

// By not having any in_pin and out_pin assignments, this plugin will
// automatically adapt to the number of channels of the track.

@init
  
  // Converts dB values to float gain factors. Divisions
  // are slow, so: dB/20 --> dB * 1/20 --> dB * 0.05
  function dBToGain (decibels) (pow(10, decibels * 0.05));
  
  // Scales the slider's normalized [-1,+1] value to the
  // relative amount of decibels set in the range param.
  function sliderTodB () local (decibels)
  (
    // The amount of max. Decibels according to range parameter
    decibels = 6 * pow(2, range);
    
    // Range dB scaled by the fader value
    amount * decibels;
  );
  
@slider
  
  // Convert dB change to float gain factor
  trim = dBToGain(sliderTodB());
  
@sample
  
  // Set this to 0 to start with first input channel
  channel = 0;
  
  // Cycle through all of the plugin's input channels
  while (channel < num_ch)
  (
    // Apply trim gain to the current channel's sample
    spl(channel) *= trim;
    
    // Increment counter to next channel
    channel += 1;
  );
